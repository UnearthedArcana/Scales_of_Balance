
//__________________________________________________________________________________
//__________________________________________________________________________________
//
//								WPO - COMBINE SOME PROFS
//__________________________________________________________________________________
//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION revise_weapon_categories BEGIN

//UPDATE PROFICIENCY STRINGS_________________________________________________________
//
COPY_EXISTING ~weapprof.2da~ ~override~
	SET_2DA_ENTRY 11 2 1 RESOLVE_STR_REF (@1225)
	SET_2DA_ENTRY 11 3 1 RESOLVE_STR_REF (@1225)
	SET_2DA_ENTRY 15 2 1 RESOLVE_STR_REF (@1224)
	SET_2DA_ENTRY 16 2 1 RESOLVE_STR_REF (@1225)
	SET_2DA_ENTRY 16 3 1 RESOLVE_STR_REF (@1225)
	SET_2DA_ENTRY 17 2 1 RESOLVE_STR_REF (@1226)
	SET_2DA_ENTRY 18 2 1 RESOLVE_STR_REF (@1227)
	SET_2DA_ENTRY 20 2 1 RESOLVE_STR_REF (@1223)
	SET_2DA_ENTRY 21 2 1 RESOLVE_STR_REF (@1228)
	SET_2DA_ENTRY 22 2 1 RESOLVE_STR_REF (@1225)
	SET_2DA_ENTRY 22 3 1 RESOLVE_STR_REF (@1225)
	SET_2DA_ENTRY 24 2 1 RESOLVE_STR_REF (@1225)
	SET_2DA_ENTRY 24 3 1 RESOLVE_STR_REF (@1225)
	SET_2DA_ENTRY 27 2 1 RESOLVE_STR_REF (@1225)
	SET_2DA_ENTRY 27 3 1 RESOLVE_STR_REF (@1225)
	SET_2DA_ENTRY 28 2 1 RESOLVE_STR_REF (@1229)
	SET_2DA_ENTRY 29 2 1 RESOLVE_STR_REF (@1225)
	SET_2DA_ENTRY 29 3 1 RESOLVE_STR_REF (@1225)
BUT_ONLY

ACTION_IF GAME_IS ~bgee~ THEN BEGIN
	STRING_SET 24315 @1222
/*
	STRING_SET 25000 @1225
	STRING_SET 25004 @1224
	STRING_SET 25005 @1225
	STRING_SET 25006 @1226
	STRING_SET 25007 @1227
	STRING_SET 25010 @1228
	STRING_SET 25011 @1225
	STRING_SET 25016 @1225
	STRING_SET 25017 @1229
	STRING_SET 25018 @1225
*/
	STRING_SET 25024 ~ ~
	STRING_SET 25029 ~ ~
	STRING_SET 25035 ~ ~
	STRING_SET 25040 ~ ~
	STRING_SET 25042 ~ ~
END
ACTION_IF GAME_IS ~tob bgt tutu bg2ee eet~ THEN BEGIN
	STRING_SET 9588 @1222
/*
	STRING_SET 31113 @1225
	STRING_SET 31117 @1224
	STRING_SET 31118 @1225
	STRING_SET 31119 @1226
	STRING_SET 31120 @1227
	STRING_SET 31122 @1228
	STRING_SET 31123 @1225
	STRING_SET 31128 @1225
	STRING_SET 31129 @1229
	STRING_SET 31130 @1225
*/
	STRING_SET 34146 ~ ~
	STRING_SET 34151 ~ ~
	STRING_SET 34158 ~ ~
	STRING_SET 34163 ~ ~
	STRING_SET 34165 ~ ~
END
ACTION_IF GAME_IS ~iwdee~ THEN BEGIN
	STRING_SET 9588 @1222
/*
	STRING_SET 36807 @1224 // 2-H sw
	STRING_SET 36811 @1226 // scimitar
	STRING_SET 36813 @1227 // dagger
	STRING_SET 36819 @1228 // spear
	STRING_SET 37721 @1229 // shortbow
	STRING_SET 36799 @1225 // bastard sw
	STRING_SET 36809 @1225 // katana
	STRING_SET 36821 @1225 // halberd
	STRING_SET 37719 @1225 // longbow
	STRING_SET 37723 @1225 // dart
*/
	STRING_SET 36800 ~ ~ // bastard sw
	STRING_SET 36810 ~ ~ // katana
	STRING_SET 36822 ~ ~ // halberd
	STRING_SET 37720 ~ ~ // longbow
	STRING_SET 37724 ~ ~ // dart
END
//__________________________________________________________________________________


//MERGE CLUBS > MACES_______________________________________________________________
//
COPY_EXISTING_REGEXP GLOB ~.*\.itm~ ~override~
	PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
	READ_BYTE 0x31 prof
	READ_BYTE 0x1b spell_weapon 
		PATCH_IF prof = 101 BEGIN
			PATCH_IF ((~%spell_weapon%~ BOR ~0b11111110~) = ~0b11111110~) BEGIN // if undispellable flag not set
				WRITE_BYTE 0x31 115
/*
				PATCH_IF FILE_EXISTS_IN_GAME ~d5__fnp_usability.d5~ BEGIN
					WRITE_SHORT 0x1c 17
				END
*/
			END
		END
	END
BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 233 match_parameter2 = 101 parameter2 = 115 END
IF_EXISTS BUT_ONLY
	
COPY_EXISTING ~weapprof.2da~ ~override~
	COUNT_2DA_COLS cols
	FOR (col = 4; col < cols; col += 1) BEGIN
	    READ_2DA_ENTRY 17 col cols club
	    READ_2DA_ENTRY 21 col cols mace
	    PATCH_IF (%mace% > %club%) BEGIN
	    	SET_2DA_ENTRY 17 col cols %mace%
		END
		SET_2DA_ENTRY 21 col cols ~0~
	END
//__________________________________________________________________________________

//MERGE HALBERDS > SPEARS___________________________________________________________
//
COPY_EXISTING_REGEXP GLOB ~.*\.itm~ ~override~
	PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
	READ_BYTE 0x31 prof
	READ_BYTE 0x1b spell_weapon 
		PATCH_IF prof = 99 BEGIN
			PATCH_IF ((~%spell_weapon%~ BOR ~0b11111110~) = ~0b11111110~) BEGIN // if undispellable flag not set
				WRITE_BYTE 0x31 98
/*
				PATCH_IF FILE_EXISTS_IN_GAME ~d5__fnp_usability.d5~ BEGIN
					WRITE_SHORT 0x1c 44
				END
*/
			END
		END
	END
BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 233 match_parameter2 = 99 parameter2 = 98 END
IF_EXISTS BUT_ONLY
	
COPY_EXISTING ~weapprof.2da~ ~override~
	COUNT_2DA_COLS cols
	FOR (col = 4; col < cols; col += 1) BEGIN
	    READ_2DA_ENTRY 18 col cols spear
	    READ_2DA_ENTRY 19 col cols halberd
	    PATCH_IF (%halberd% > %spear%) BEGIN
	    	SET_2DA_ENTRY 18 col cols %halberd%
		END
	    SET_2DA_ENTRY 19 col cols ~0~
	END
//__________________________________________________________________________________

//MERGE DARTS > DAGGERS_____________________________________________________________
//
COPY_EXISTING_REGEXP GLOB ~.*\.itm~ ~override~
	PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
	READ_BYTE 0x31 prof
	READ_BYTE 0x1b spell_weapon 
		PATCH_IF prof = 106 BEGIN
			PATCH_IF ((~%spell_weapon%~ BOR ~0b11111110~) = ~0b11111110~) BEGIN // if undispellable flag not set
				WRITE_BYTE 0x31 96
/*
				PATCH_IF FILE_EXISTS_IN_GAME ~d5__fnp_usability.d5~ BEGIN
					WRITE_SHORT 0x1c 16
				END
*/
			END
		END
	END
BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 233 match_parameter2 = 106 parameter2 = 96 END
IF_EXISTS BUT_ONLY
	
COPY_EXISTING ~weapprof.2da~ ~override~
	COUNT_2DA_COLS cols
	FOR (col = 4; col < cols; col += 1) BEGIN
	    READ_2DA_ENTRY 15 col cols dagger
	    READ_2DA_ENTRY 26 col cols darts
	    PATCH_IF (%darts% > %dagger%) BEGIN
	    	SET_2DA_ENTRY 15 col cols %darts%
		END
	    SET_2DA_ENTRY 26 col cols ~0~
	END
//__________________________________________________________________________________

//MERGE KATANAS > SCIMITARS_________________________________________________________
//
COPY_EXISTING_REGEXP GLOB ~.*\.itm~ ~override~
	PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
	READ_BYTE 0x31 prof
	READ_BYTE 0x1b spell_weapon 
		PATCH_IF prof = 94 BEGIN
			PATCH_IF ((~%spell_weapon%~ BOR ~0b11111110~) = ~0b11111110~) BEGIN // if undispellable flag not set
				WRITE_BYTE 0x31 95
/*
				PATCH_IF FILE_EXISTS_IN_GAME ~d5__fnp_usability.d5~ BEGIN
					WRITE_SHORT 0x1c 29
				END
*/
			END
		END
	END
BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 233 match_parameter2 = 94 parameter2 = 95 END
IF_EXISTS BUT_ONLY
	
COPY_EXISTING ~weapprof.2da~ ~override~
	COUNT_2DA_COLS cols
	FOR (col = 4; col < cols; col += 1) BEGIN
	    READ_2DA_ENTRY 14 col cols scimitar
	    READ_2DA_ENTRY 13 col cols katana
	    PATCH_IF (%katana% > %scimitar%) BEGIN
	    	SET_2DA_ENTRY 14 col cols %katana%
		END
	    SET_2DA_ENTRY 13 col cols ~0~
	END
//__________________________________________________________________________________

//MERGE LONG BOWS > SHORT BOWS______________________________________________________
//
COPY_EXISTING_REGEXP GLOB ~.*\.itm~ ~override~
	PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
	READ_BYTE 0x31 prof
	READ_BYTE 0x1b spell_weapon 
		PATCH_IF prof = 104 BEGIN
			PATCH_IF ((~%spell_weapon%~ BOR ~0b11111110~) = ~0b11111110~) BEGIN // if undispellable flag not set
				WRITE_BYTE 0x31 105
			END
		END
	END
BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 233 match_parameter2 = 104 parameter2 = 105 END
IF_EXISTS BUT_ONLY
	
COPY_EXISTING ~weapprof.2da~ ~override~
	COUNT_2DA_COLS cols
	FOR (col = 4; col < cols; col += 1) BEGIN
	    READ_2DA_ENTRY 25 col cols shortbow
	    READ_2DA_ENTRY 24 col cols longbow
	    PATCH_IF (%longbow% > %shortbow%) BEGIN
	    	SET_2DA_ENTRY 25 col cols %longbow%
		END
	    SET_2DA_ENTRY 24 col cols ~0~
	END
//__________________________________________________________________________________

//MERGE BASTARD SWORDS > 2-HAND SWORDS______________________________________________
//
COPY_EXISTING_REGEXP GLOB ~.*\.itm~ ~override~
	PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
	READ_BYTE 0x31 prof
	READ_BYTE 0x18 handed 
	READ_BYTE 0x1b spell_weapon 
		PATCH_IF prof = 89 BEGIN
			PATCH_IF ((~%spell_weapon%~ BOR ~0b11111110~) = ~0b11111110~) BEGIN // if undispellable flag not set
				PATCH_IF ((~%handed%~ BOR ~0b11111101~) = ~0b11111101~) BEGIN // if 2-handed flag not set
					WRITE_BYTE 0x31 90
/*
					PATCH_IF FILE_EXISTS_IN_GAME ~d5__fnp_usability.d5~ BEGIN
						WRITE_SHORT 0x1c 20
					END
*/
				END
				PATCH_IF ((~%handed%~ BOR ~0b11111101~) = ~0b11111111~) BEGIN // if 2-handed flag IS set
					WRITE_BYTE 0x31 93
/*
					PATCH_IF FILE_EXISTS_IN_GAME ~d5__fnp_usability.d5~ BEGIN
						WRITE_SHORT 0x1c 57
					END
*/
				END
			END
		END
	END
BUT_ONLY
COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 233 match_parameter2 = 89 parameter2 = 90 END
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 233 match_parameter2 = 89 parameter2 = 93 END
IF_EXISTS BUT_ONLY
	
COPY_EXISTING ~weapprof.2da~ ~override~
	COUNT_2DA_COLS cols
	FOR (col = 4; col < cols; col += 1) BEGIN
	    READ_2DA_ENTRY 9 col cols longsword
	    READ_2DA_ENTRY 8 col cols bastard
	    PATCH_IF ((VARIABLE_IS_SET %bastard%) and (VARIABLE_IS_SET %longsword%)) BEGIN
	      PATCH_IF (%bastard% > %longsword%) BEGIN
	    	SET_2DA_ENTRY 9 col cols %bastard%
		  END
		END
	    SET_2DA_ENTRY 8 col cols ~0~
	END
//__________________________________________________________________________________


// NEED FnP COMPATIBILITY
// ... replace 181 effects for substituted categories
// ... right?
// ... ya. that's gonna be annoying

CLEAR_ARRAYS

END // end function
