
//__________________________________________________________________________________
//__________________________________________________________________________________
//
//								WPO - STYLE CHANGES
//__________________________________________________________________________________
//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION revise_weapon_styles BEGIN

//COPY MARKER FILE_________________________________________________________________
//
COPY ~scales_of_balance/lib/markers/d5_marker.d5~ ~override/d5__styles.d5~
//__________________________________________________________________________________


//UPDATE STYLE STRINGS______________________________________________________________
//
/*
COPY_EXISTING ~weapprof.2da~ ~override~
	READ_2DA_ENTRY 31 2 1 2h_string 
	READ_2DA_ENTRY 31 3 1 2h_desc   
	READ_2DA_ENTRY 32 2 1 sns_string 
	READ_2DA_ENTRY 32 3 1 sns_desc   
	READ_2DA_ENTRY 33 2 1 sws_string 
	READ_2DA_ENTRY 33 3 1 sws_desc   
	READ_2DA_ENTRY 34 2 1 dw_string 
	READ_2DA_ENTRY 34 3 1 dw_desc   

	STRING_SET %2h_string% @1250
	STRING_SET %sns_string% @1251
	STRING_SET %sws_string% @1252
	STRING_SET %dw_string% @1253
	STRING_SET %2h_desc% @1245
	STRING_SET %sns_desc% @1246
	STRING_SET %sws_desc% @1247
	STRING_SET %dw_desc% @1249
*/

COPY_EXISTING ~weapprof.2da~ ~override~
	SET_2DA_ENTRY 31 2 1 RESOLVE_STR_REF (@1250)
	SET_2DA_ENTRY 31 3 1 RESOLVE_STR_REF (@1245)  
	SET_2DA_ENTRY 32 2 1 RESOLVE_STR_REF (@1251) 
	SET_2DA_ENTRY 32 3 1 RESOLVE_STR_REF (@1246)  
	SET_2DA_ENTRY 33 2 1 RESOLVE_STR_REF (@1252) 
	SET_2DA_ENTRY 33 3 1 RESOLVE_STR_REF (@1247)  
	SET_2DA_ENTRY 34 2 1 RESOLVE_STR_REF (@1253)
	SET_2DA_ENTRY 34 3 1 RESOLVE_STR_REF (@1249) 

//__________________________________________________________________________________


//AMEND WEAPPROF.2da________________________________________________________________
//
COPY_EXISTING ~weapprof.2da~ ~override~
	// Wizard
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 4 0	//	2-hand
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 4 0	//	shield
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 4 0	//	single
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 4 0	//	dual-wield
	// Fighter
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 5 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 5 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 5 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 5 2
	// Cleric
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 6 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 6 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 6 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 6 0
	// Thief
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 7 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 7 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 7 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 7 2
	// Bard
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 8 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 8 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 8 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 8 2
	// Paladin
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 9 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 9 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 9 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 9 2
	// Druid
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 10 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 10 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 10 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 10 2
	// Ranger
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 11 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 11 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 11 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 11 2
	// Fighter/Mage
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 12 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 12 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 12 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 12 0
	// Fighter/Cleric
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 13 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 13 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 13 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 13 0
	// Fighter/Thief
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 14 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 14 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 14 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 14 2
	// Fighter/Mage/Thief
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 15 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 15 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 15 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 15 2
	// Mage/Thief
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 16 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 16 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 16 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 16 0
	// Cleric/Mage
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 17 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 17 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 17 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 17 0
	// Cleric/Thief
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 18 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 18 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 18 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 18 2
	// Fighter/Druid
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 19 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 19 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 19 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 19 2
	// Fighter/Mage/Cleric
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 20 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 20 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 20 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 20 0
	// Ranger/Cleric
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 21 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 21 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 21 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 21 2
	// Monk
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 51 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 51 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 51 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 51 2
	// shaman
	PATCH_IF FILE_EXISTS_IN_GAME ~enginest.2da~ BEGIN
	 SET_2DA_ENTRY_LATER ~#weapprof~ 31 60 2
	 SET_2DA_ENTRY_LATER ~#weapprof~ 32 60 0
	 SET_2DA_ENTRY_LATER ~#weapprof~ 33 60 2
	 SET_2DA_ENTRY_LATER ~#weapprof~ 34 60 2
	END
	// done
	SET_2DA_ENTRIES_NOW ~#weapprof~ 1
	REPLACE_TEXTUALLY ~^ID ~ ~%TAB%%TAB%%TAB%%TAB%%TAB%%TAB%ID ~
//__________________________________________________________________________________


//CLASS/WEAPPROF LISTS______________________________________________________________
//
COPY_EXISTING ~kitlist.2da~ ~override~
//  COUNT_2DA_COLS cols
  READ_2DA_ENTRIES_NOW ~r2en_kitlist~ 10
  FOR (row = 2; row < r2en_kitlist; row += 1) BEGIN
    READ_2DA_ENTRY_FORMER ~r2en_kitlist~ row 6 prof_col
    READ_2DA_ENTRY_FORMER ~r2en_kitlist~ row 8 class_num
    PATCH_IF (class_num == 3) OR (class_num == 8) OR (class_num == 17) BEGIN 		//	clerics
      DEFINE_ASSOCIATIVE_ARRAY d5_clerics_array BEGIN "%prof_col%" => "%class_num%" END
    END
    PATCH_IF (class_num == 11) OR (class_num == 16) OR (class_num == 21) BEGIN 		//	druids
      DEFINE_ASSOCIATIVE_ARRAY d5_druids_array BEGIN "%prof_col%" => "%class_num%" END
    END
    PATCH_IF (class_num == 1) OR (class_num == 19) BEGIN 							//	wizards
      DEFINE_ASSOCIATIVE_ARRAY d5_wizards_array BEGIN "%prof_col%" => "%class_num%" END
    END
    PATCH_IF (class_num == 4) OR (class_num == 13) OR (class_num == 15) OR (class_num == 10) BEGIN 	//	thieves
      DEFINE_ASSOCIATIVE_ARRAY d5_thieves_array BEGIN "%prof_col%" => "%class_num%" END
    END
    PATCH_IF (class_num == 5) BEGIN 												//	bards
      DEFINE_ASSOCIATIVE_ARRAY d5_bards_array BEGIN "%prof_col%" => "%class_num%" END
    END
    PATCH_IF (class_num == 12) OR (class_num == 18) BEGIN 							//	rangers
      DEFINE_ASSOCIATIVE_ARRAY d5_rangers_array BEGIN "%prof_col%" => "%class_num%" END
    END
    PATCH_IF (class_num == 6) BEGIN 												//	paladins
      DEFINE_ASSOCIATIVE_ARRAY d5_paladins_array BEGIN "%prof_col%" => "%class_num%" END
    END
    PATCH_IF (class_num == 2) BEGIN 												//	fighters
      DEFINE_ASSOCIATIVE_ARRAY d5_fighters_array BEGIN "%prof_col%" => "%class_num%" END
    END
    PATCH_IF (class_num == 7) OR (class_num == 14) OR (class_num == 17) BEGIN 		//	fighter/mages, cleric/mages, F/M/Cs
      DEFINE_ASSOCIATIVE_ARRAY d5_mc_mages_array BEGIN "%prof_col%" => "%class_num%" END
    END
    PATCH_IF (class_num == 0) BEGIN 												//	monks
      DEFINE_ASSOCIATIVE_ARRAY d5_monks_array BEGIN "%prof_col%" => "%class_num%" END
    END
  END
BUT_ONLY

//STYLES FOR KIT CLERICS____________________________________________________________
//
COPY_EXISTING ~weapprof.2da~ ~override~
  COUNT_2DA_COLS cols 
  PHP_EACH d5_clerics_array AS kit_col => class BEGIN
    SET_2DA_ENTRY 28 (%kit_col% + 1) cols 2 // 2Hand
    SET_2DA_ENTRY 29 (%kit_col% + 1) cols 2 // shield
    SET_2DA_ENTRY 30 (%kit_col% + 1) cols 2 // single
    SET_2DA_ENTRY 31 (%kit_col% + 1) cols 0 // dual-wield
  END
BUT_ONLY

//STYLES FOR KIT DRUIDS_____________________________________________________________
//
COPY_EXISTING ~weapprof.2da~ ~override~
  COUNT_2DA_COLS cols 
  PHP_EACH d5_druids_array AS kit_col => class BEGIN
    SET_2DA_ENTRY 28 (%kit_col% + 1) cols 2 // 2Hand
    SET_2DA_ENTRY 29 (%kit_col% + 1) cols 0 // shield
    SET_2DA_ENTRY 30 (%kit_col% + 1) cols 2 // single
    SET_2DA_ENTRY 31 (%kit_col% + 1) cols 2 // dual-wield
  END
BUT_ONLY

//STYLES FOR KIT WIZARDS____________________________________________________________
//
COPY_EXISTING ~weapprof.2da~ ~override~
  COUNT_2DA_COLS cols 
  PHP_EACH d5_wizards_array AS kit_col => class BEGIN
    SET_2DA_ENTRY 28 (%kit_col% + 1) cols 0 // 2Hand
    SET_2DA_ENTRY 29 (%kit_col% + 1) cols 0 // shield
    SET_2DA_ENTRY 30 (%kit_col% + 1) cols 0 // single
    SET_2DA_ENTRY 31 (%kit_col% + 1) cols 0 // dual-wield
  END
BUT_ONLY

//STYLES FOR KIT MC MAGES___________________________________________________________
//
COPY_EXISTING ~weapprof.2da~ ~override~
  COUNT_2DA_COLS cols 
  PHP_EACH d5_mc_mages_array AS kit_col => class BEGIN
    SET_2DA_ENTRY 28 (%kit_col% + 1) cols 2 // 2Hand
    SET_2DA_ENTRY 29 (%kit_col% + 1) cols 0 // shield
    SET_2DA_ENTRY 30 (%kit_col% + 1) cols 2 // single
    SET_2DA_ENTRY 31 (%kit_col% + 1) cols 0 // dual-wield
  END
BUT_ONLY

//STYLES FOR KIT THIEVES____________________________________________________________
//
COPY_EXISTING ~weapprof.2da~ ~override~
  COUNT_2DA_COLS cols 
  PHP_EACH d5_thieves_array AS kit_col => class BEGIN
    SET_2DA_ENTRY 28 (%kit_col% + 1) cols 0 // 2Hand
    SET_2DA_ENTRY 29 (%kit_col% + 1) cols 0 // shield
    SET_2DA_ENTRY 30 (%kit_col% + 1) cols 2 // single
    SET_2DA_ENTRY 31 (%kit_col% + 1) cols 2 // dual-wield
  END
BUT_ONLY

//STYLES FOR KIT BARDS______________________________________________________________
//
COPY_EXISTING ~weapprof.2da~ ~override~
  COUNT_2DA_COLS cols 
  PHP_EACH d5_bards_array AS kit_col => class BEGIN
    SET_2DA_ENTRY 28 (%kit_col% + 1) cols 2 // 2Hand
    SET_2DA_ENTRY 29 (%kit_col% + 1) cols 2 // shield
    SET_2DA_ENTRY 30 (%kit_col% + 1) cols 2 // single
    SET_2DA_ENTRY 31 (%kit_col% + 1) cols 2 // dual-wield
  END
BUT_ONLY

//STYLES FOR KIT RANGERS____________________________________________________________
//
COPY_EXISTING ~weapprof.2da~ ~override~
  COUNT_2DA_COLS cols 
  PHP_EACH d5_rangers_array AS kit_col => class BEGIN
    SET_2DA_ENTRY 28 (%kit_col% + 1) cols 2 // 2Hand
    SET_2DA_ENTRY 29 (%kit_col% + 1) cols 0 // shield
    SET_2DA_ENTRY 30 (%kit_col% + 1) cols 2 // single
    SET_2DA_ENTRY 31 (%kit_col% + 1) cols 2 // dual-wield
  END
BUT_ONLY

//STYLES FOR KIT PALADINS___________________________________________________________
//
COPY_EXISTING ~weapprof.2da~ ~override~
  COUNT_2DA_COLS cols 
  PHP_EACH d5_paladins_array AS kit_col => class BEGIN
    SET_2DA_ENTRY 28 (%kit_col% + 1) cols 2 // 2Hand
    SET_2DA_ENTRY 29 (%kit_col% + 1) cols 2 // shield
    SET_2DA_ENTRY 30 (%kit_col% + 1) cols 2 // single
    SET_2DA_ENTRY 31 (%kit_col% + 1) cols 2 // dual-wield
  END
BUT_ONLY

//STYLES FOR KIT FIGHTERS___________________________________________________________
//
COPY_EXISTING ~weapprof.2da~ ~override~
  COUNT_2DA_COLS cols 
  PHP_EACH d5_fighters_array AS kit_col => class BEGIN
    SET_2DA_ENTRY 28 (%kit_col% + 1) cols 2 // 2Hand
    SET_2DA_ENTRY 29 (%kit_col% + 1) cols 2 // shield
    SET_2DA_ENTRY 30 (%kit_col% + 1) cols 2 // single
    SET_2DA_ENTRY 31 (%kit_col% + 1) cols 2 // dual-wield
  END
BUT_ONLY

//STYLES FOR KIT MONKS______________________________________________________________
//
COPY_EXISTING ~weapprof.2da~ ~override~
  COUNT_2DA_COLS cols 
  PHP_EACH d5_monks_array AS kit_col => class BEGIN
    SET_2DA_ENTRY 28 (%kit_col% + 1) cols 0 // 2Hand
    SET_2DA_ENTRY 29 (%kit_col% + 1) cols 0 // shield
    SET_2DA_ENTRY 30 (%kit_col% + 1) cols 2 // single
    SET_2DA_ENTRY 31 (%kit_col% + 1) cols 2 // dual-wield
  END
BUT_ONLY
//__________________________________________________________________________________


//CHANGE BASIC BONUSES______________________________________________________________
//
COPY ~scales_of_balance/profs/stylbonu2.2da~ ~override/stylbonu.2da~
//__________________________________________________________________________________



//PREP SPLPROT.2DA__________________________________________________________________
//
APPEND ~splprot.2da~ ~D5_115_STYLE%TAB%115%TAB%-1%TAB%8~ UNLESS ~D5_115_STYLE~
APPEND ~splprot.2da~ ~D5_115_NSTYLE%TAB%115%TAB%-1%TAB%9~ UNLESS ~D5_115_NSTYLE~

COPY_EXISTING ~splprot.2da~ ~override~
	COUNT_2DA_COLS cols
	READ_2DA_ENTRIES_NOW rows cols   
	FOR (row = 1; row < rows; ++row) BEGIN 
	  READ_2DA_ENTRY_FORMER rows row 0 ~stat~ 
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~D5_115_STYLE~ BEGIN
	    SET 115_style = %row%
	  END
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~D5_115_NSTYLE~ BEGIN
	    SET 115_nstyle = %row%
	  END
	END
BUT_ONLY
//__________________________________________________________________________________


//2-HAND WEAPON_____________________________________________________________________
//
INCLUDE ~scales_of_balance/ini/styles.ini~

ACTION_IF (2H_weap_apr == 1) BEGIN

COPY ~scales_of_balance/profs/stylbonu3.2da~ ~override/stylbonu.2da~

/*
- give +.5 APR for two pips

2-hand items:
- while equipped, trigger .eff to run 232 effect

.eff:
- every 6 seconds, cast ths1

ths1:
- 326, conditionally cast ths2 if >= 2 pips 2hs

ths2: 
- +0.5 APR, 7 seconds

shields:
- on equip, 321 cancel ths2
- while equipped, 206 prot. from ths1, ths2

1-handed weapons: 
- on equip, 321 cancel ths2
*/

 APPEND ~splprot.2da~ ~D5_SPEC_2HS%TAB%111%TAB%2%TAB%4~		//	specialized or better in 2HS

 COPY_EXISTING ~splprot.2da~ ~override~
	COUNT_2DA_COLS cols // amount of columns
	READ_2DA_ENTRIES_NOW rows cols // read all file into memory  
	FOR (row = 1; row < rows; ++row) BEGIN // iterate over rows
	  READ_2DA_ENTRY_FORMER rows row 0 ~stat~ // read column value
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~D5_SPEC_2HS~ BEGIN
	    SET 2hs_spec = %row%
	  END
	END
 BUT_ONLY

 COPY ~scales_of_balance/profs/d5_ths.eff~ ~override~
 COPY ~scales_of_balance/profs/d5_ths1.spl~ ~override~
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 326 parameter1 = 0 parameter2 = %2hs_spec% timing = 1 duration = 0 END
 COPY ~scales_of_balance/profs/d5_ths2.spl~ ~override~

 COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (~%SOURCE_SIZE%~ > ~0x71~) BEGIN
	READ_SHORT 0x1c type
	READ_BYTE 0x31 prof
	READ_BYTE 0x18 handed
	PATCH_IF (%prof% = 98) OR	// spear
			(%prof% = 99) OR	// halberd
			(%prof% = 89) OR	// bastard sword
			(%prof% = 93) OR	// 2-hand sword
			(%prof% = 92) OR	// axe
			(%prof% = 94) OR	// katana
			(%prof% = 102) BEGIN	 // staff
	  PATCH_IF ((%handed% BAND 0b00000010) = 0b00000010) BEGIN 	//	2-handed
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 177 target = 1 parameter1 = 0 parameter2 = 2 timing = 2 STR_VAR resource = ~d5_ths~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 146 target = 1 parameter1 = 0 parameter2 = 1 timing = 1 STR_VAR resource = ~d5_ths1~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 STR_VAR resource = ~d5_ths2~ END
	  END
	END

	PATCH_IF ((%handed% BOR 0b11111101) = 0b11111101) BEGIN 	//	1-handed
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 resist_dispel = 0 STR_VAR resource = ~d5_ths2~ END
	END

	PATCH_IF (%prof% = 106) OR	// darts
			(%type% = 27) OR	// crossbows
			(%type% = 15) OR	// bows
			(%type% = 18) BEGIN	// slings
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 resist_dispel = 0 STR_VAR resource = ~d5_ths2~ END
	END

	PATCH_IF (%type% = 12) BEGIN	//	shields
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 parameter1 = (0 - 1) timing = 2 resist_dispel = 0 STR_VAR resource = ~d5_ths1~ END
//add	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 parameter1 = (0 - 1) timing = 2 resist_dispel = 0 STR_VAR resource = ~d5_ths2~ END
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 2 resist_dispel = 0 STR_VAR resource = ~d5_ths2~ END
	END
  END
 BUT_ONLY

END
//__________________________________________________________________________________


//SWS & DW__________________________________________________________________________
//

//APPEND ~splprot.2da~ ~D5_115_BIT6%TAB%115%TAB%50%TAB%8~		//	50 = 32 = bit6 of extraproficiency20
//APPEND ~splprot.2da~ ~D5_115_BIT7%TAB%115%TAB%100%TAB%8~  	//	100 = 64 = bit7 of extraproficiency20
//APPEND ~splprot.2da~ ~D5_115_NOT7%TAB%115%TAB%100%TAB%9~  	//	100 = 64 = bit7 of extraproficiency20

APPEND ~splprot.2da~ ~D5_PROF_SWS%TAB%113%TAB%1%TAB%4~		//	proficient or better in SWS
APPEND ~splprot.2da~ ~D5_SPEC_SWS%TAB%113%TAB%2%TAB%4~		//	specialized or better in SWS
APPEND ~splprot.2da~ ~D5_PROF_TWF%TAB%114%TAB%1%TAB%4~		//	proficient or better in TWF
APPEND ~splprot.2da~ ~D5_NPROF_TWF%TAB%114%TAB%0%TAB%1~		//	not proficient in TWF

COPY_EXISTING ~splprot.2da~ ~override~
	COUNT_2DA_COLS cols 
	READ_2DA_ENTRIES_NOW rows cols  
	FOR (row = 1; row < rows; ++row) BEGIN 
	  READ_2DA_ENTRY_FORMER rows row 0 ~stat~ 
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~D5_PROF_SWS~ BEGIN
	    SET sws_prof = %row%
	  END
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~D5_SPEC_SWS~ BEGIN
	    SET sws_spec = %row%
	  END
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~D5_PROF_TWF~ BEGIN
	    SET twf_prof = %row%
	  END
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~D5_NPROF_TWF~ BEGIN
	    SET twf_nprof = %row%
	  END
	END
BUT_ONLY

COPY ~scales_of_balance/profs/d5_swsd.eff~ ~override~										// 232 casting swsm
COPY ~scales_of_balance/profs/d5_swsd.spl~ ~override~
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 326 parameter1 = (64 << 24) parameter2 = %115_nstyle% END
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 326 parameter1 = (64 << 24) parameter2 = %115_style% STR_VAR resource = ~d5_twf4~ END
COPY ~scales_of_balance/profs/d5_swsm.eff~ ~override~										// 232 casting swsm
COPY ~scales_of_balance/profs/d5_swsm.spl~ ~override~
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 326 parameter1 = (64 << 24) parameter2 = %115_nstyle% END
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 326 parameter1 = (64 << 24) parameter2 = %115_style% STR_VAR resource = ~d5_twf4~ END
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 326 parameter1 = (64 << 24) parameter2 = %115_style% STR_VAR resource = ~d5_twf1~ END
COPY ~scales_of_balance/profs/d5_swsl.eff~ ~override~										// 232 casting swsl
COPY ~scales_of_balance/profs/d5_swsl.spl~ ~override~
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 326 parameter1 = (64 << 24) parameter2 = %115_nstyle% END
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 326 parameter1 = (64 << 24) parameter2 = %115_style% STR_VAR resource = ~d5_twf5~ END
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 326 parameter1 = (64 << 24) parameter2 = %115_style% STR_VAR resource = ~d5_twf2~ END
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 326 parameter1 = (64 << 24) parameter2 = %115_style% STR_VAR resource = ~d5_twf3~ END
COPY ~scales_of_balance/profs/d5_sws2.spl~ ~override~
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 326 parameter1 = 0 parameter2 = %sws_prof% timing = 1 duration = 0 STR_VAR resource = ~d5_sws4~ END
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 326 parameter1 = 0 parameter2 = %sws_spec% timing = 1 duration = 0 STR_VAR resource = ~d5_sws3~ END
COPY ~scales_of_balance/profs/d5_sws3.spl~ ~override~										// APR
COPY ~scales_of_balance/profs/d5_sws4.spl~ ~override~										// save vs. breath

COPY ~scales_of_balance/profs/d5_twf1.spl~ ~override~										// thac0 penalty +1
//	LPF ALTER_EFFECT INT_VAR silent = 1 parameter1 = 2 END
COPY ~scales_of_balance/profs/d5_twf2.spl~ ~override~										// thac0 penalty +1
//	LPF ALTER_EFFECT INT_VAR silent = 1 parameter1 = 2 END
COPY ~scales_of_balance/profs/d5_twf3.spl~ ~override~										// thac0 penalty +2
//	LPF ALTER_EFFECT INT_VAR silent = 1 parameter1 = 2 END
COPY ~scales_of_balance/profs/d5_twf4.spl~ ~override~										// melee AC penalty -1: med. weaps
COPY ~scales_of_balance/profs/d5_twf5.spl~ ~override~										// melee AC penalty -2: lg. weaps

COPY ~scales_of_balance/profs/d5_swsn.spl~ ~override~

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (~%SOURCE_SIZE%~ > ~0x71~) BEGIN
	READ_SHORT 0x1c type
	READ_SHORT 0x22 anim
	READ_BYTE 0x31 prof
	PATCH_IF (%anim% = 12627) BEGIN 						// appearance = longsword
	  PATCH_IF (%prof% = 95) || (%prof% = 91)  BEGIN 		// prof = short sword or scim/wak/nin
		WRITE_SHORT 0x22 21331 								// ninja-to! change to short sword anim.
		WRITE_SHORT 0x1c 19 								// change to small swords type
	  END
	END
  END
BUT_ONLY

ACTION_IF FILE_EXISTS_IN_GAME ~d5__fnp_usability.d5~ BEGIN
  COPY_EXISTING_REGEXP GLOB ~^[Dd]5_[Uu].+\.spl$~ ~override~
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 181 match_parameter1 = 69 parameter1 = 51 END  //  halberds
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 181 match_parameter1 = 57 parameter1 = 52 END  //  2-hand swords
  BUT_ONLY

  COPY_EXISTING_REGEXP GLOB ~^[Dd]5_[Uu].+\.spl$~ ~override~
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 181 match_parameter1 = 22 parameter1 = 69 END  //  bastard swords
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 181 match_parameter1 = 30 parameter1 = 57 END  //  katanas
  BUT_ONLY

  COPY_EXISTING_REGEXP GLOB ~^[Dd]5_[Uu].+\.spl$~ ~override~
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 181 match_parameter1 = 51 parameter1 = 22 END  //  halberds
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 181 match_parameter1 = 52 parameter1 = 30 END  //  2-hand swords
  BUT_ONLY
END

//here, move bastard swords to item type 69 and katanas to item type 57.
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (~%SOURCE_SIZE%~ > ~0x71~) BEGIN
	READ_SHORT 0x1c type
	READ_SHORT 0x22 anim
	READ_BYTE 0x31 prof
	READ_BYTE 0x18 handed
	PATCH_IF ((%handed% BOR 0b11111101) = 0b11111101) BEGIN 	//	1-handed
	  PATCH_IF (%type% = 20) BEGIN 								// type = large swords
		PATCH_IF (%prof% = 94) BEGIN 							// katana prof - vanilla katanas
		  WRITE_SHORT 0x1c 57
		END	
		PATCH_IF (%prof% = 95) BEGIN 							// scimitar prof
		  PATCH_IF (%anim% = 13139) BEGIN 						// katana appearance - SoB katanas
			WRITE_SHORT 0x1c 57
		  END
		  PATCH_IF (%anim% = 12627) || (%anim% = 21331) BEGIN 	// long/short sword appearance - wak or ninja-to
			WRITE_SHORT 0x1c 19
		  END
		END
		PATCH_IF (%anim% = 12371) BEGIN 						// bastard sword appearance
		  WRITE_SHORT 0x1c 69
		END
	  END
	END
  END
BUT_ONLY	  

//Update all stores to accept new itemtypes... looks like this was adapted from either CDTweaks or IR
//
COPY_EXISTING_REGEXP	~.*\.sto~	override
	READ_SHORT	0x8	type
	READ_LONG	0x2c	buy_off
	READ_LONG	0x30	buy_num
	SET	buy_add = 0	//	Number of categories to add, to update offsets
	SET	add_category = 0	//	Initialize check variable
	FOR	(i = 0; i < 3; ++i)	BEGIN	SET $add_category(~%i%~) = 1	END	//	Initialize check variable
	FOR	(i = 0; i < buy_num; ++i)	BEGIN
		READ_BYTE	(buy_off + i * 0x4)	category
		PATCH_MATCH	category WITH
			15 16 17 18 19 20 21 22 23 24 25 26 27 29 30 WHEN add_category = 0	BEGIN	// Match any Weapon Categories
				++add_category
			END
			69	BEGIN	//	Match "Bastard Sword" Category - No Unnecessary Duplication
				SET add_category_0 = 0
			END
			57	BEGIN	//	Match "Greatsword" Category - No Unnecessary Duplication
				SET add_category_1 = 0
			END
			DEFAULT
		END
	END
	//	Add categories
	PATCH_IF	add_category	BEGIN
		PATCH_IF	add_category_0	BEGIN
			INSERT_BYTES	(buy_off + buy_num * 0x4)	0x4
			WRITE_BYTE	(buy_off + buy_num * 0x4)	69
			++buy_add
		END
		PATCH_IF	add_category_1	BEGIN
			INSERT_BYTES	(buy_off + buy_num * 0x4)	0x4
			WRITE_BYTE	(buy_off + buy_num * 0x4)	57
			++buy_add
		END
		//	Update offsets
		WRITE_LONG  0x30    (THIS + buy_add)
		READ_LONG	0x34	items_off
		PATCH_IF	items_off >= buy_off	BEGIN	
			WRITE_LONG  0x34    (THIS + (buy_add * 0x04))
		END
		READ_LONG	0x4c	drink_off
		PATCH_IF	drink_off >= buy_off	BEGIN
			WRITE_LONG  0x4c    (THIS + (buy_add * 0x04))
		END
		READ_LONG	0x70	cures_off
		PATCH_IF	cures_off >= buy_off	BEGIN
			WRITE_LONG  0x70    (THIS + (buy_add * 0x04))
		END
	END
BUT_ONLY
//__________________________________________________________________________________


COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (~%SOURCE_SIZE%~ > ~0x71~) BEGIN
	READ_SHORT 0x1c type
	READ_SHORT 0x22 anim
	READ_BYTE 0x31 prof
	READ_BYTE 0x18 handed
	PATCH_IF ((%handed% BOR 0b11111101) = 0b11111101) BEGIN 		//	1-handed
	  PATCH_IF (%type% = 16) BEGIN 								// daggers = S
		SPRINT $d5_weapon_sizes("%SOURCE_RES%") "small"
	  END
	  PATCH_IF (%type% = 19) BEGIN 								// small swords = ...M?
		SPRINT $d5_weapon_sizes("%SOURCE_RES%") "medium"
	  END
	  PATCH_IF (%type% = 21) BEGIN 								// hammers = M
		SPRINT $d5_weapon_sizes("%SOURCE_RES%") "medium"
	  END
	  PATCH_IF (%type% = 17) BEGIN 								// maces = M
		SPRINT $d5_weapon_sizes("%SOURCE_RES%") "medium"
	  END
	  PATCH_IF (%type% = 20) BEGIN 								// large swords = L
		SPRINT $d5_weapon_sizes("%SOURCE_RES%") "large"
	  END
	  PATCH_IF (%type% = 69) BEGIN 								// bastard swords = L
		SPRINT $d5_weapon_sizes("%SOURCE_RES%") "large"
	  END
	  PATCH_IF (%type% = 23) BEGIN 								// flails = L
		SPRINT $d5_weapon_sizes("%SOURCE_RES%") "large"
	  END
	  PATCH_IF (%type% = 25) BEGIN 								// axes = L
		SPRINT $d5_weapon_sizes("%SOURCE_RES%") "large"
	  END
	  PATCH_IF !(FILE_EXISTS_IN_GAME ~d5__fnp_usability.d5~) BEGIN
		PATCH_IF (%type% = 22) BEGIN 							// morning stars = M
		  SPRINT $d5_weapon_sizes("%SOURCE_RES%") "medium"
		END
		PATCH_IF (%type% = 29) BEGIN 							// spears = L
		  SPRINT $d5_weapon_sizes("%SOURCE_RES%") "large"
		END
		PATCH_IF (%type% = 26) BEGIN 							// quarterstaff = L
		  SPRINT $d5_weapon_sizes("%SOURCE_RES%") "large"
		END
		PATCH_IF (%type% = 57) BEGIN 							// FnP katanas = L
		  SPRINT $d5_weapon_sizes("%SOURCE_RES%") "large"
		END
	  END
	  PATCH_IF (FILE_EXISTS_IN_GAME ~d5__fnp_usability.d5~) BEGIN
		PATCH_IF NOT (MOD_IS_INSTALLED "item_rev.tp2" "17") BEGIN 	// non-IR
		  PATCH_IF (%type% = 26) BEGIN 							// FnP clubs = M
			SPRINT $d5_weapon_sizes("%SOURCE_RES%") "medium"
		  END
		END
		PATCH_IF (MOD_IS_INSTALLED "item_rev.tp2" "17") BEGIN 		// IR
		  PATCH_IF (%type% = 26) BEGIN 							// FnP IR clubs = ...M?
			SPRINT $d5_weapon_sizes("%SOURCE_RES%") "medium"
		  END
		END
		PATCH_IF (%type% = 29) BEGIN 							// FnP scimitars = L?
		  SPRINT $d5_weapon_sizes("%SOURCE_RES%") "large"
		END
		PATCH_IF (%type% = 22) BEGIN 							// FnP bastard swords = L
		  SPRINT $d5_weapon_sizes("%SOURCE_RES%") "large"
		END
		PATCH_IF (%type% = 30) BEGIN 							// FnP katanas = L
		  SPRINT $d5_weapon_sizes("%SOURCE_RES%") "large"
		END
	  END
	END
  END
BUT_ONLY

ACTION_PHP_EACH d5_weapon_sizes AS weap => size BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%weap%.itm~ BEGIN
	PRINT ~%weap% = %size%~
	ACTION_IF (~%size%~ STRING_EQUAL_CASE ~small~) BEGIN
	  COPY_EXISTING ~%weap%.itm~ ~override~
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 177 target = 1 parameter1 = 0 parameter2 = 2 timing = 2 STR_VAR resource = ~d5_swsd~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 146 target = 1 parameter1 = 0 parameter2 = 1 timing = 1 STR_VAR resource = ~d5_sws2~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 146 target = 1 parameter1 = 0 parameter2 = 1 timing = 4 duration = 1 STR_VAR resource = ~d5_swsd~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 318 target = 1 parameter1 = (64 << 24) parameter2 = %115_style% timing = 2 STR_VAR resource = ~d5_sws3~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 318 target = 1 parameter1 = (64 << 24) parameter2 = %115_style% timing = 2 STR_VAR resource = ~d5_sws4~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 parameter1 = (0 - 1) timing = 2 resist_dispel = 0 STR_VAR resource = ~d5_twf4~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 parameter1 = (0 - 1) timing = 2 resist_dispel = 0 STR_VAR resource = ~d5_twf5~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 parameter1 = (0 - 1) timing = 2 resist_dispel = 0 STR_VAR resource = ~d5_twf3~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 STR_VAR resource = ~d5_sws3~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 STR_VAR resource = ~d5_sws4~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 STR_VAR resource = ~d5_twf3~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 STR_VAR resource = ~d5_twf4~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 STR_VAR resource = ~d5_twf5~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 233 target = 1 parameter1 = (32 << 24) parameter2 = (115 + (0x10000 * 1)) timing = 2 END
	END
	ACTION_IF (~%size%~ STRING_EQUAL_CASE ~medium~) BEGIN
	  COPY_EXISTING ~%weap%.itm~ ~override~
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 177 target = 1 parameter1 = 0 parameter2 = 2 timing = 2 STR_VAR resource = ~d5_swsm~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 146 target = 1 parameter1 = 0 parameter2 = 1 timing = 1 STR_VAR resource = ~d5_sws2~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 146 target = 1 parameter1 = 0 parameter2 = 1 timing = 4 duration = 1 STR_VAR resource = ~d5_swsm~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 326 target = 1 parameter1 = (64 << 24) parameter2 = %115_style% timing = 1 STR_VAR resource = ~d5_twf4~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 318 target = 1 parameter1 = (64 << 24) parameter2 = %115_style% timing = 2 STR_VAR resource = ~d5_sws3~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 318 target = 1 parameter1 = (64 << 24) parameter2 = %115_style% timing = 2 STR_VAR resource = ~d5_sws4~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 318 target = 1 parameter1 = 0 parameter2 = %twf_nprof% timing = 2 STR_VAR resource = ~d5_twf4~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 318 target = 1 parameter1 = 0 parameter2 = %twf_nprof% timing = 2 STR_VAR resource = ~d5_twf5~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 parameter1 = (0 - 1) timing = 2 resist_dispel = 0 STR_VAR resource = ~d5_twf3~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 STR_VAR resource = ~d5_sws3~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 STR_VAR resource = ~d5_sws4~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 STR_VAR resource = ~d5_twf3~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 STR_VAR resource = ~d5_twf4~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 STR_VAR resource = ~d5_twf5~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 233 target = 1 parameter1 = (32 << 24) parameter2 = (115 + (0x10000 * 1)) timing = 2 END
	END
	ACTION_IF (~%size%~ STRING_EQUAL_CASE ~large~) BEGIN
	  COPY_EXISTING ~%weap%.itm~ ~override~
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 177 target = 1 parameter1 = 0 parameter2 = 2 timing = 2 STR_VAR resource = ~d5_swsl~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 146 target = 1 parameter1 = 0 parameter2 = 1 timing = 4 duration = 1 STR_VAR resource = ~d5_swsl~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 318 target = 1 parameter1 = (64 << 24) parameter2 = %115_style% timing = 2 STR_VAR resource = ~d5_sws3~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 318 target = 1 parameter1 = (64 << 24) parameter2 = %115_style% timing = 2 STR_VAR resource = ~d5_sws4~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 318 target = 1 parameter1 = 0 parameter2 = %twf_nprof% timing = 2 STR_VAR resource = ~d5_twf5~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 parameter1 = (0 - 1) timing = 2 resist_dispel = 0 STR_VAR resource = ~d5_twf4~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 STR_VAR resource = ~d5_sws3~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 STR_VAR resource = ~d5_sws4~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 STR_VAR resource = ~d5_twf4~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 STR_VAR resource = ~d5_twf5~ END
		LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 233 target = 1 parameter1 = (32 << 24) parameter2 = (115 + (0x10000 * 1)) timing = 2 END
	END
  END
END

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (~%SOURCE_SIZE%~ > ~0x71~) BEGIN
	READ_SHORT 0x1c type
	READ_BYTE 0x31 prof
	READ_BYTE 0x18 handed
	PATCH_IF ((%handed% BAND 0b00000010) = 0b00000010) BEGIN 	//	2-handed
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 resist_dispel = 0 STR_VAR resource = ~d5_sws3~ END
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 resist_dispel = 0 STR_VAR resource = ~d5_sws4~ END
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 resist_dispel = 0 STR_VAR resource = ~d5_twf1~ END
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 resist_dispel = 0 STR_VAR resource = ~d5_twf2~ END
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 resist_dispel = 0 STR_VAR resource = ~d5_twf3~ END
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 resist_dispel = 0 STR_VAR resource = ~d5_twf4~ END
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 resist_dispel = 0 STR_VAR resource = ~d5_twf5~ END
	END
	PATCH_IF (%type% = 24) OR	// darts
			(%type% = 27) OR	// crossbows
			(%type% = 15) OR	// bows
			(%type% = 18) BEGIN	// slings
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 resist_dispel = 0 STR_VAR resource = ~d5_sws3~ END
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 resist_dispel = 0 STR_VAR resource = ~d5_sws4~ END
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 resist_dispel = 0 STR_VAR resource = ~d5_twf1~ END
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 resist_dispel = 0 STR_VAR resource = ~d5_twf2~ END
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 resist_dispel = 0 STR_VAR resource = ~d5_twf3~ END
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 resist_dispel = 0 STR_VAR resource = ~d5_twf4~ END
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 resist_dispel = 0 STR_VAR resource = ~d5_twf5~ END
	END
	PATCH_IF (%prof% = 96) BEGIN	// daggers (and maybe darts)
	  READ_LONG 0x64 abil_offset
	  READ_SHORT 0x68 abil_number
	  WHILE (%abil_number% > 0) BEGIN
		SET abil_number = (%abil_number% - 1)
		READ_SHORT (%abil_offset% + (0x38 * %abil_number%)) ranged
		PATCH_IF (%ranged% = 2 || %ranged% = 4) BEGIN 	// ranged weapon
		  PATCH_IF !(type = 24) BEGIN
		    LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 1 match_parameter1 = 2 match_parameter2 = 1 parameter1 = 7 END
		  END
		  PATCH_IF (type = 24) BEGIN
		    LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 1 match_parameter1 = 3 match_parameter2 = 1 parameter1 = 8 END
		  END
		END
	  END
	END
	PATCH_IF (%type% = 12) BEGIN	//	shields
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 parameter1 = (0 - 1) timing = 2 resist_dispel = 0 STR_VAR resource = ~d5_swsm~ END
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 parameter1 = (0 - 1) timing = 2 resist_dispel = 0 STR_VAR resource = ~d5_swsl~ END
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 2 resist_dispel = 0 STR_VAR resource = ~d5_sws3~ END
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 resist_dispel = 0 STR_VAR resource = ~d5_sws4~ END
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 resist_dispel = 0 STR_VAR resource = ~d5_twf1~ END
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 resist_dispel = 0 STR_VAR resource = ~d5_twf2~ END
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 resist_dispel = 0 STR_VAR resource = ~d5_twf3~ END
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 resist_dispel = 0 STR_VAR resource = ~d5_twf4~ END
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 321 target = 1 timing = 1 resist_dispel = 0 STR_VAR resource = ~d5_twf5~ END
	END
  END
BUT_ONLY

ACTION_FOR_EACH class IN ~clabfi01~ ~clabrn01~ ~clabth01~ ~clabpr01~ ~clabdr01~ ~clabma01~ BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%class%.2da~ BEGIN
	APPEND ~%class%.2da~ ~ABILITY    AP_D5_SWSN  ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****  ~ 
  END
END

//__________________________________________________________________________________


//SHIELD BASH_______________________________________________________________________
//
/*

- shields set a 177/232 while equipped, firing at nearest enemy when attacked, casts sns1
- sns1 uses 326, checks SnS >= 2 on original caster, if so cast sns2
- sns2 = damage etc.

*/

APPEND ~splprot.2da~ ~D5_SPEC_SNS%TAB%112%TAB%2%TAB%4~

COPY_EXISTING ~splprot.2da~ ~override~
	COUNT_2DA_COLS cols // amount of columns
	READ_2DA_ENTRIES_NOW rows cols // read all file into memory  
	FOR (row = 1; row < rows; ++row) BEGIN // iterate over rows
	  READ_2DA_ENTRY_FORMER rows row 0 ~stat~ // read column value
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~D5_SPEC_SNS~ BEGIN
	    SET sns_spec = %row%
	  END
	END
BUT_ONLY

ADD_PROJECTILE ~scales_of_balance/profs/d5_sns.pro~
COPY ~scales_of_balance/profs/d5_sns.eff~ ~override~						// cast sns when attacked
COPY ~scales_of_balance/profs/d5_sns1.spl~ ~override~						// cast sns2 if spec.
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 326 parameter1 = 0 parameter2 = %sns_spec% END
COPY ~scales_of_balance/profs/d5_sns2.spl~ ~override~						// stun/damage/etc.
	LPF ALTER_SPELL_HEADER INT_VAR projectile = %d5_sns% END

COPY ~scales_of_balance/profs/d5_snst.spl~ ~override~						// thieves' immunity to shield bash
COPY ~scales_of_balance/profs/d5_snst.eff~ ~override~						// thieves' immunity to shield bash

APPEND ~clabth01.2da~ ~ABILITY    AP_D5_SNST  ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****  ~ 
COPY_EXISTING ~kitlist.2da~ ~override~
  COUNT_2DA_ROWS ~9~ "rows"
  FOR ( index = 31 ; index < rows ; index = index + 1 ) BEGIN
	READ_2DA_ENTRY %index% 5 9 clab
	READ_2DA_ENTRY %index% 8 9 class
	DEFINE_ASSOCIATIVE_ARRAY d5_sns_thieves BEGIN "%clab%" => "%class%" END
  END
BUT_ONLY
ACTION_PHP_EACH d5_sns_thieves AS lemon => lime BEGIN
  ACTION_IF (%lime% = 3) BEGIN 
	ACTION_IF FILE_EXISTS_IN_GAME ~%lemon%.2da~ BEGIN
	  APPEND ~%lemon%.2da~ ~ABILITY    AP_D5_SNST  ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****  ~ 
	END
  END
END

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (~%SOURCE_SIZE%~ > ~0x71~) BEGIN
	READ_SHORT 0x1c type
	PATCH_IF (%type% = 12) BEGIN
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 177 target = 1 parameter1 = 0 parameter2 = 2 timing = 2 resist_dispel = 0 probability1 = 100 STR_VAR resource = ~d5_sns~ END
	END
  END
BUT_ONLY
//__________________________________________________________________________________

CLEAR_ARRAYS

END // end function